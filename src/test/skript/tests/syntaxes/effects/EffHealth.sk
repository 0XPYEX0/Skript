test "health effect":
    clear all entities
    set {_i} to diamond sword
    assert {_i}'s damage value is 0 with "new item durability failed"
    damage {_i} by 50
    assert {_i}'s damage value is 50 with "damage item failed"
    repair {_i} by 49
    assert {_i}'s damage value is 1 with "repair item failed"

    spawn cow at location(0, 64, 0, world "world")
    set {_m} to last spawned cow
    assert health of {_m} is 5 with "default cow health failed"
    damage {_m} by 0.5
    assert health of {_m} is 4.5 with "damage cow failed"
    damage {_m} by 99
    assert health of {_m} is 0 with "damage cow failed"
    heal {_m} by 1
    assert health of {_m} is 1 with "heal cow failed"
    heal {_m} by 0.5
    assert health of {_m} is 1.5 with "heal cow failed"
    heal {_m} by 99
    assert health of {_m} is 5 with "heal cow failed"

    set {_v::*} to "%minecraft version%" split by "."
    set {_result} to "%{_v::2}%%{_v::3} ? 0%" parsed as number
    parse if {_result} >= 204:
        send "1.20.4+ and test damage cause" to console
        damage {_m} by 2 hearts with damage cause fall
        assert health of {_m} is 3 with "damage cow failed"
        send "Check passed" to console
    clear all entities
